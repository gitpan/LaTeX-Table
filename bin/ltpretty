#!/usr/bin/perl

#############################################################################
#   $Author: markus $
#     $Date: 2008-09-18 13:55:10 +0200 (Thu, 18 Sep 2008) $
# $Revision: 1007 $
#############################################################################

use strict;
use warnings;

use LaTeX::Table;
use Carp;

use version; our $VERSION = $LaTeX::Table::VERSION;

my $line_id = 0;
my @input;
my $paramline;

while (my $line =  <>) {
    chomp $line;
    $line =~ s{\s+}{ }xmsg;
    $line =~ s{\A \s* | \\ | \s* \z}{}xmsg;
    if ($line_id == 0) { #first line contains the parameters
        $paramline = $line;
    }
    else {
        push @input, $line;
    }
    $line_id++;
}

_say( join q{}, map { "\n % $_" } ($paramline, @input) );

@input = map { [ split m{ & }xms ] }  @input;

# store the paramline in a hash
my %params = map { _parse_param($_) } split m{ ; }xms, $paramline;

my $table = LaTeX::Table->new({
        header  => [ $input[0] ],
        data    => [ @input[1 .. $#input ] ],
        caption => q{},
        label   => q{},
        %params,
    });

_say("\n" . $table->generate_string());

sub _parse_param {
    my ( $s ) = @_;
    if ( $s =~ m{ \A (.*?) = (.*) \z}xms) {
        return $1 => $2;
    }
    else {
        croak q{Invalid parameter line};
    }
    return;
}

sub _say {
    my ( $s ) = @_;
    print "$s\n" or croak q{Can't print to stdout.};
    return;
}

__END__

=head1 NAME

ltpretty

=head1 SYNOPSIS

  ltpretty < data.tex

=head1 DESCRIPTION

Formats the input nicely. The first line must contain the parameters, the
second the header, the following lines the data. Columns seperated by C<&>.
You can use this to pipe selected code in your editor to this script.

=head1 EXAMPLE

  theme=NYC;label=test;position=htb
  Header Col1 & Header Col2
  Data Col1 & Data Col2

=head1 CONFIGURATION AND ENVIRONMENT

C<ltpretty> does not support configuration files or environment variables.

=head1 DEPENDENCIES

L<LaTeX::Table> 

=head1 BUGS AND LIMITATIONS

No bugs have been reported. 

Please report any bugs or feature requests to
C<bug-latex-table@rt.cpan.org>, or through the web interface at
L<http://rt.cpan.org>. 

=head1 AUTHOR

Markus Riester  C<< <mriester@gmx.de> >>

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2006-2008, Markus Riester C<< <mriester@gmx.de> >>. 
All rights reserved.

This module is free software; you can redistribute it and/or
modify it under the same terms as Perl itself. See L<perlartistic>.

=head1 DISCLAIMER OF WARRANTY

BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY APPLICABLE LAW. EXCEPT WHEN
OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
PROVIDE THE SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE
ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE OF THE SOFTWARE IS WITH
YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL
NECESSARY SERVICING, REPAIR, OR CORRECTION.

IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE
LIABLE TO YOU FOR DAMAGES, INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL,
OR CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE
THE SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING
RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR THIRD PARTIES OR A
FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF
SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF
SUCH DAMAGES.

=cut

# vim: ft=perl sw=4 ts=4 expandtab
